plugins{
    id 'java-library'
    id 'io.qameta.allure' version "2.12.0"
}

repositories {
    mavenCentral()
}

def jUnitVersion = "5.13.1",
    jUnitPlatformLauncherVersion = "1.13.1",
    restAssuredVersion = "5.5.5",
    assertJVersion = "4.0.0-M1",
    allureVersion = '2.29.0',
    slf4jVersion = "2.0.17"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        autoconfigure.set(true)
        aspectjWeaver.set(true)
        frameworks {
            junit5{
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    testImplementation(
        "org.junit.jupiter:junit-jupiter-api:$jUnitVersion",
        "io.rest-assured:rest-assured:$restAssuredVersion",
        "io.rest-assured:json-schema-validator:$restAssuredVersion",
        "org.assertj:assertj-core:$assertJVersion",
        "io.qameta.allure:allure-rest-assured:$allureVersion",
        "com.github.javafaker:javafaker:1.0.2"
    )
    testRuntimeOnly(
        "org.junit.jupiter:junit-jupiter-engine:$jUnitVersion",
        "org.junit.platform:junit-platform-launcher:$jUnitPlatformLauncherVersion",
        "org.slf4j:slf4j-simple:$slf4jVersion"
    )
}

test{
    useJUnitPlatform()
    systemProperties(System.getProperties())

    testLogging {
        lifecycle {
            events( "started", "skipped", "failed", "standard_error", "standard_out")
            exceptionFormat "short"
        }
        showStandardStreams = true
    }

}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}
